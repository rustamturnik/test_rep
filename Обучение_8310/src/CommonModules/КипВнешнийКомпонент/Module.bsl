// Получить объект инструментов (см. раздел ИНСТРУМЕНТЫ)
//
// Возвращаемое значение:
//  Объект "Addin.ETP.Tools"
//
Функция ПолучитьИнструменты() Экспорт
	
	ПодключитьВнешнийКомпонент();
	Возврат СоздатьИнструменты();
	
КонецФункции // ПолучитьИнструменты()

// Создать объект инструментов (см. раздел ИНСТРУМЕНТЫ)
//
// Возвращаемое значение:
//  Объект "Addin.ETP.Tools"
//
Функция СоздатьИнструменты()
	
	Возврат Новый("Addin.ETP.Tools");
	
КонецФункции // СоздатьИнструменты()

// Подключить внешнюю компоненту КИП
//
Процедура ПодключитьВнешнийКомпонент()
	
	ОтладкаРазрешена = Ложь;
	
	Если Не ОтладкаРазрешена Тогда
		ИмяКомпонента = "ОбщийМакет.КипВнешнийКомпонент";
	
		Если Не ПодключитьВнешнююКомпоненту(ИмяКомпонента, "ETP") Тогда
			#Если Клиент Тогда
				УстановитьВнешнююКомпоненту(ИмяКомпонента);
			#КонецЕсли
			
			Если Не ПодключитьВнешнююКомпоненту(ИмяКомпонента, "ETP") Тогда
				ВызватьИсключение "Ошибка подключения внешнего компонента.";
			КонецЕсли;
		КонецЕсли;
	Иначе
		УспешноПодключено = ПодключитьВнешнююКомпоненту(
		#Если Клиент Тогда
			"C:\Projects\ETPAddin\Bin\Debug-Win32\AddInNative\AddInNative.dll",
		#Иначе
			"C:\Projects\ETPAddin\Bin\Debug-x64\AddInNative\AddInNative.dll",
		#КонецЕсли
			"ETP",
			ТипВнешнейКомпоненты.Native);
		
		Если Не УспешноПодключено Тогда
			ВызватьИсключение "Ошибка подключения внешнего компонента.";
		КонецЕсли;
	КонецЕсли;
	
	//Инструменты = СоздатьИнструменты();
	//ПодключеннаяВерсия = ВерсияКомпонента(Инструменты);
	//ТребуемаяВерсия = "2.0.5.43";
	//
	//Если ПодключеннаяВерсия <> ТребуемаяВерсия Тогда
	//	ВызватьИсключение
	//		"Версия подключенного внешнего компонента ("
	//		+ ПодключеннаяВерсия
	//		+ ") не совпадает с требуемой версией ("
	//		+ ТребуемаяВерсия
	//		+ "). Для устранения несоответствия версий, выгрузите всех клиентов"
	//		" и перезагрузите сервер 1С:Предприятия этой информационной базы.";
	//КонецЕсли;
	
КонецПроцедуры // ПодключитьВнешнийКомпонент()

// Выполнить метод внешнего компонента
//
// Параметры:
//  Объект - "Addin.ETP.*", экземпляр класса внешнего компонента
//  Имя - Строка, имя выполняемого метода
//  Параметры - Структура, содержит имена и значения параметров
//
// Возвращаемое значение:
//   Произвольный - результат, возвращаемый методом
//
Функция ВыполнитьМетод(Объект, Имя, Параметры = Неопределено)
	
	ИменаПараметров = "";
	
	Если Параметры <> Неопределено Тогда
		Для каждого Параметр Из Параметры Цикл
			Если Не ПустаяСтрока(ИменаПараметров) Тогда
				ИменаПараметров = ИменаПараметров + ", ";
			КонецЕсли;
			
			ИменаПараметров = ИменаПараметров + "Параметры." + Параметр.Ключ;
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		Возврат Вычислить("Объект." + Имя + "(" + ИменаПараметров + ")");
	Исключение
		Если Объект <> Неопределено И Объект.ЕстьОшибка() Тогда
			ВызватьИсключение Объект.ОписаниеОшибки();
		Иначе
			ВызватьИсключение;
		КонецЕсли;
	КонецПопытки;
	
КонецФункции // ВыполнитьМетод()

// Приостановить текущий поток на указанное количество миллисекунд
//
// Параметры:
//  Инструменты - Объект внешнего компонента (см. ПолучитьИнструменты())
//  Длительность - Число, количество миллисекунд, на которое приостановить поток
//
Процедура Пауза(Инструменты, Длительность) Экспорт
	
	Параметры = Новый Структура("Длительность", Длительность);
	ВыполнитьМетод(Инструменты, "Пауза", Параметры);
	
КонецПроцедуры // Пауза()