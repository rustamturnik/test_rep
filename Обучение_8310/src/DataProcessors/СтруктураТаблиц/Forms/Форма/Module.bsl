
&НаКлиенте
Процедура ВывестиСтруктуруТаблиц(Команда)
	
	МассивИменМетаданных = Новый Массив();
	
	// Если нужна структура таблиц только некоторых объектов конфигурации
	// их можно перечислить в массиве имен метаданных
	
	//МассивИменМетаданных.Добавить("Справочник.Номенклатура");
	//МассивИменМетаданных.Добавить("Документ.РеализацияТоваровУслуг");
	
	ТекстовыйДокумент = ПолучитьСтруктуруТаблицНаСервере(МассивИменМетаданных);
	
	ТекстовыйДокумент.Показать("Структура хранения базы данных");
	
	Закрыть();
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруТаблицНаСервере(МассивИменМетаданных)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	// Таблица значений, содержащая таблицы указанного объекта
	Таблицы = ПолучитьСтруктуруХраненияБазыДанных(МассивИменМетаданных);
	
	Для Каждого Таблица Из Таблицы Цикл
		
		ТекстовыйДокумент.ДобавитьСтроку("");
		
		ИмяТаблицы = ?(Таблица.ИмяТаблицы = "", Таблица.Метаданные, Таблица.ИмяТаблицы);
		ИмяТаблицы = ?(ИмяТаблицы = "", Таблица.ИмяТаблицыХранения, ИмяТаблицы);
		
		ТекстовыйДокумент.ДобавитьСтроку("Таблица: " + ИмяТаблицы + 
										 ", Имя таблицы хранения: " + Таблица.ИмяТаблицыХранения + 
										 ", Назначение: " + Таблица.Назначение);
	
		ТекстовыйДокумент.ДобавитьСтроку("- поля: "); 
		ПоляТаблицы = Таблица.Поля;
		Для Каждого Поле Из ПоляТаблицы Цикл
			
		ТекстовыйДокумент.ДобавитьСтроку("   " + Поле.ИмяПоля + 
										 " (" + Поле.ИмяПоляХранения + ")");
			
		КонецЦикла;	
		
		ТекстовыйДокумент.ДобавитьСтроку("- индексы: "); 
		ИндексыТаблицы = Таблица.Индексы;
		Для Каждого Индекс Из ИндексыТаблицы Цикл
			
			ТекстовыйДокумент.ДобавитьСтроку("   " + Индекс.ИмяИндексаХранения);
			
			СтрокаИндекса = "";
			СтрокаХраненияИндекса = "";
			ПоляИндекса = Индекс.Поля;
			Для Каждого ПолеИндекса Из ПоляИндекса Цикл
				
				Если СтрокаИндекса <> "" Тогда
					СтрокаИндекса = СтрокаИндекса + " + ";
				КонецЕсли;
				
				ИмяПоляИндекса = ?(ПолеИндекса.ИмяПоля = "", ПолеИндекса.ИмяПоляХранения, ПолеИндекса.ИмяПоля);
				СтрокаИндекса = СтрокаИндекса + ИмяПоляИндекса;
				
				Если СтрокаХраненияИндекса <> "" Тогда
					СтрокаХраненияИндекса = СтрокаХраненияИндекса + " + ";
				КонецЕсли;	
				СтрокаХраненияИндекса = СтрокаХраненияИндекса + ПолеИндекса.ИмяПоляХранения;
				
			КонецЦикла;	
			
			ТекстовыйДокумент.ДобавитьСтроку("      " + СтрокаИндекса + 
											 " (" + СтрокаХраненияИндекса + ")");
					
		КонецЦикла;	
		
	КонецЦикла;	
	
	Возврат ТекстовыйДокумент;
	
КонецФункции	